import org.ajoberstar.gradle.git.release.opinion.Strategies
import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'org.ajoberstar:gradle-git:1.4.0-rc.1'
    }
}

apply plugin: 'org.ajoberstar.release-base'
release {
    grgit = Grgit.open()
    versionStrategy Strategies.FINAL
    versionStrategy Strategies.PRE_RELEASE
    defaultVersionStrategy Strategies.SNAPSHOT
}

repositories {
    mavenLocal()
    jcenter()
    maven() {
        url "http://dl.bintray.com/serenity/maven/"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
}

ext {
    serenityCoreVersion = "2.0.23"

    // Bintray configuration
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayRepository = 'maven'
    bintrayPackage = 'serenity-jira'
    projectDescription = 'Serenity JIRA Integration'

    if (!project.hasProperty("bintrayUsername")) {
        bintrayUsername = 'wakaleo'
    }
    if (!project.hasProperty("bintrayApiKey")) {
        bintrayApiKey = ''
    }
}

group = 'net.serenity-bdd'

allprojects {
    apply plugin: 'project-report'

    configurations.all {
        resolutionStrategy {
            // fail fast on dependency convergence problems
            failOnVersionConflict()
            force 'commons-collections:commons-collections:3.2.2',
                    "xml-apis:xml-apis:1.4.01",
                    "org.seleniumhq.selenium:selenium-api:3.9.1",
                    "commons-codec:commons-codec:1.10",
                    "com.google.guava:guava:23.0",
                    "com.google.code.gson:gson:2.8.2",
                    'net.bytebuddy:byte-buddy:1.7.9',
                    'org.eclipse.jetty:jetty-http:9.4.8.v20171121',
                    'org.eclipse.jetty:jetty-util:9.4.8.v20171121',
                    'org.eclipse.jetty:jetty-io:9.4.8.v20171121',
                    'org.eclipse.jetty.websocket:websocket-client:9.4.8.v20171121',
                    'org.eclipse.jetty.websocket:websocket-common:9.4.8.v20171121',
                    'org.eclipse.jetty.websocket:websocket-api:9.4.8.v20171121',
                    'org.eclipse.jetty:jetty-client:9.4.8.v20171121',
                    'org.eclipse.jetty:jetty-xml:9.4.8.v20171121',
                    'org.apache.httpcomponents:httpcore:4.4.7',
                    'org.apache.httpcomponents:httpmime:4.5.4',
                    'org.apache.commons:commons-text:1.2',
                    'org.apache.httpcomponents:httpclient:4.5.4'

        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadoc(type: Javadoc, overwrite: true) {
        source = sourceSets.main.allJava
        options.memberLevel = JavadocMemberLevel.PUBLIC
        options.addStringOption('views')
        options.addStringOption('all')
        options.addStringOption('quiet')
        options.addStringOption('qualify')
        options.addStringOption('Xdoclint:none')
        options.encoding = 'UTF-8'
        classpath = configurations.compile
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    bintray {
        user = bintrayUsername //this usually comes form gradle.properties file in ~/.gradle
        key = bintrayApiKey //this usually comes form gradle.properties file in ~/.gradle
        publications = ['mavenJava'] // see publications closure
        pkg {
            repo = 'maven'
            userOrg = 'serenity'
            name = 'serenity-jira'
            desc = 'Serenity JIRA libraries'
            licenses = ['Apache-2.0']
            labels = ['serenity', 'bdd']
        }
    }

    tasks.withType(GroovyCompile) {
        options.encoding = "UTF-8" // for Java compilation
        groovyOptions.encoding = "UTF-8" // for Groovy compilation
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:-options"
        options.encoding = 'UTF-8'
    }
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {

                println "Publishing $project.name:$project.group:$project.version"

                from components.java
                groupId 'net.serenity-bdd'

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name "$project.name"
                        description "$projectDescription"

                        url 'https://github.com/serenity-bdd/serenity-jira'
                        scm {
                            url 'scm:git:git@github.com:serenity-bdd/serenity-jira.git'
                            connection 'scm:git:git@github.com:serenity-bdd/serenity-jira.git'
                            developerConnection 'scm:git:git@github.com:serenity-bdd/serenity-jira.git'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'johnsmart'
                                name 'John Ferguson Smart'
                                email 'john.smart@wakaleo.com'
                            }
                        }
                    }
                    // Preserve compile-scope dependencies
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each() {
                        it.scope*.value = 'compile'
                    }
                }
            }
        }

        test {
            exclude '**/*$*'
            exclude '**/integration/**'
            exclude '**/localhost/**'
            exclude '**/samples/**'
            exclude '**/*Sample*'
            minHeapSize = "128m"
            maxHeapSize = "512m"
            maxParallelForks = Runtime.runtime.availableProcessors()
        }

        task integrationTests(type: Test) {
            exclude '**/*$*'
            exclude '**/samples/**'
            exclude '**/*Sample*'
            exclude '**/integration/browsers**'
            exclude '**/localhost/**'
            include '**/integration/**'
            reports.junitXml.destination = "${buildDir}/reports/integration-tests"
            reports.html.destination = "${buildDir}/reports/integration-tests"
            minHeapSize = "128m"
            maxHeapSize = "2048m"
            jvmArgs '-XX:MaxPermSize=256m'
            maxParallelForks = Runtime.runtime.availableProcessors()
        }

        test {
            testLogging {
                exceptionFormat = 'full'
                showStandardStreams = true
            }
        }

        integrationTests {
            testLogging {
                exceptionFormat = 'full'
            }
        }

        task javadoc(type: Javadoc, overwrite: true) {
            source = sourceSets.main.allJava
            options.memberLevel = JavadocMemberLevel.PUBLIC
            options.addStringOption('views')
            options.addStringOption('all')
            options.addStringOption('quiet')
            options.addStringOption('qualify')
            options.addStringOption('Xdoclint:none')
            options.encoding = 'UTF-8'
            classpath = configurations.compile
        }

        check.dependsOn -= integrationTests


        dependencies {
            compile("net.serenity-bdd:serenity-core:$serenityCoreVersion") {
                exclude group: "org.seleniumhq.selenium", module: "htmlunit-driver"
            }
            compile "com.google.inject:guice:4.2.0"
            compile "com.google.guava:guava:23.0"
            compile "org.slf4j:slf4j-api:1.7.25"
            compile "org.codehaus.groovy:groovy-all:2.4.12"
            compile("com.googlecode.lambdaj:lambdaj:2.3.3") {
                exclude group: "org.objenesis", module: "objenesis"
            }
            testCompile("ch.qos.logback:logback-classic:1.0.13") {
                exclude group: "org.slf4j", module: "slf4j-api"
            }

            testCompile "junit:junit:4.12"
            testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
                exclude group: "junit"
                exclude group: "org.codehaus.groovy", module: "groovy-all"
            }
        }

        task copyDeps(type: Copy) {
            from configurations.runtime + configurations.testCompile
            into project.projectDir.path + "/lib"
        }
    }
}
